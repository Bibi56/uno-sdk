import static org.gradle.internal.os.OperatingSystem.*

plugins {
    id 'maven'
    id 'java'
    id 'java-library'
    id "org.jetbrains.kotlin.jvm" version "1.3.41"
    id "com.github.johnrengelman.shadow" version '5.1.0'
}

// jitpack
group = 'com.github.kotlin_graphics'

ext {
    moduleName = "${group}.uno"
    kot = 'org.jetbrains.kotlin:kotlin'
    kx = "com.github.kotlin-graphics"
    kotlin_version = '1.3.41'
    kotlintest_version = '3.3.3'
    vkk_version = '76f2ba33afd38716c0a3036359b895f1e93dff29'
    gln_version = '6ce8058b1b904e50a2d22c5e918c2047086044ba'
    gli_version = '414aef014802ee274ca41a32ebcea1c54ef449a7'
    glm_version = '62dff2baa0b4cab738d55d7e41dee4c796e6cdfc'
    unsigned_version = 'a530a8f63eda75a499e06beac468f0398526f5a4'
    kool_version = '1d9a690fd33e62621d55bcb6843042390a21ae31'
    lwjgl_version = "3.2.2"
    lwjgl_natives = current() == WINDOWS ? "windows" : current() == LINUX ? "linux" : "macos"
}

dependencies {

    implementation "$kot-stdlib-jdk8"
    implementation "$kot-reflect"
}

allprojects {
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'https://jitpack.io' }
    }
}

compileKotlin {
    // Enable Kotlin compilation to Java 8 class files with method parameter name metadata
    kotlinOptions {
        jvmTarget = "1.8"
//        javaParameters = true
    }
    // As per https://stackoverflow.com/a/47669720
    // See also https://discuss.kotlinlang.org/t/kotlin-support-for-java-9-module-system/2499/9
    destinationDir = compileJava.destinationDir
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
//        javaParameters = true
    }
}

compileJava {
    dependsOn(':compileKotlin')
    doFirst {
        options.compilerArgs = ['--module-path', classpath.asPath,]
        classpath = files()
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
//    archives javadocJar
}

jar {
    inputs.property("moduleName", moduleName)
//    manifest.attributes('Automatic-Module-Name': moduleName)
}

shadowJar.archiveClassifier = 'all'
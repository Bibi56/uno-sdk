import static org.gradle.internal.os.OperatingSystem.*

plugins {
    id 'maven'
    id 'java'
    id 'java-library'
    id "org.jetbrains.kotlin.jvm" version "1.3.10"
    id "com.github.johnrengelman.shadow" version '2.0.4'
}

// jitpack
group = 'com.github.kotlin-graphics'

ext{
    moduleName = 'com.github.kotlin_graphics.uno'
    kotlin_version = '1.3.10'
    kotlintest_version = '3.1.10'
    gli_version = 'ea878d63a9faf4d90cf1a065c0f37479aa814153'
    lwjgl_version = "3.2.1-SNAPSHOT"
    lwjgl_natives = current() == WINDOWS ? "windows" : current() == LINUX ? "linux" : "macos"
}

dependencies {

    ext.kotlin = "org.jetbrains.kotlin:kotlin"
    implementation "$kotlin-stdlib:$kotlin_version".toString()
    implementation "$kotlin-reflect:$kotlin_version".toString()

    ext.kx = "com.github.kotlin-graphics"
    implementation "$kx:gln:22f9809d9c6bd8b6d88aa8625c0076335e5ddd5f".toString()
    implementation "$kx:vkk:9bb5abbc9ddfe51b9d0e1c4c1b8b88fcfa07c078".toString()

    testImplementation "io.kotlintest:kotlintest-runner-junit5:$kotlintest_version".toString()

    def joglVersion = '2.3.2'
    implementation "org.jogamp.gluegen:gluegen-rt-main:$joglVersion".toString()
    implementation "org.jogamp.jogl:jogl-all-main:$joglVersion".toString()

    ["", "-glfw", "-jemalloc", "-openal", "-opengl", "-stb", "-vulkan", "-jawt"].each {
        implementation "org.lwjgl:lwjgl$it:$lwjgl_version"
        if (it != "-vulkan" && it != "-jawt")
            runtime "org.lwjgl:lwjgl$it:$lwjgl_version:natives-$lwjgl_natives"
    }
}

repositories {
    mavenCentral()
    jcenter()
    maven { setUrl("https://dl.bintray.com/kotlin/kotlin-eap") }
    maven { setUrl("https://oss.sonatype.org/content/repositories/snapshots/") }
    maven { setUrl("https://jitpack.io") }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

jar {
    inputs.property("moduleName", moduleName)
    manifest.attributes('Automatic-Module-Name': moduleName)
}
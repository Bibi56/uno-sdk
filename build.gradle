import static org.gradle.internal.os.OperatingSystem.*

plugins {
    id 'maven'
    id 'java'
    id 'java-library'
    id "org.jetbrains.kotlin.jvm" version "1.3.50"
    id "com.github.johnrengelman.shadow" version '5.1.0'
}

// jitpack
group = 'com.github.kotlin_graphics'

ext {
    moduleName = "${group}.uno"
    kot = 'org.jetbrains.kotlin:kotlin'
    kx = "com.github.kotlin-graphics"
    kotlin_version = '1.3.50'
    kotlintest_version = '3.4.0'
    vkk_version = 'b7f9ad03ab98046ee4d3684836fc9fdac73ab594'
    gln_version = '7d31a75d53594f70f9111b5617c83b1b9095a01f'
    gli_version = '0ea3295ed86b66d548dc71ef10bbf0259f49e0a7'
    glm_version = 'a484ec1181813912c5a69d3a6706a994ca138eff'
    unsigned_version = 'c81c2e39fb9e10403650c1b371b8573fc745bfb4'
    kool_version = '0ef9ad97d0c709de3cda4d7f05e74bff883b00a6'
    lwjgl_version = "3.2.3"
    lwjgl_natives = current() == WINDOWS ? "windows" : current() == LINUX ? "linux" : "macos"
}

dependencies {

    implementation "$kot-stdlib"
    implementation "$kot-reflect"
}

allprojects {
    repositories {
        mavenCentral()
        jcenter()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'https://jitpack.io' }
    }
}

compileKotlin {
    // Enable Kotlin compilation to Java 8 class files with method parameter name metadata
    kotlinOptions {
        jvmTarget = "11"
//        javaParameters = true
    }
    // As per https://stackoverflow.com/a/47669720
    // See also https://discuss.kotlinlang.org/t/kotlin-support-for-java-9-module-system/2499/9
    destinationDir = compileJava.destinationDir
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "11"
//        javaParameters = true
    }
}

compileJava {
    dependsOn(':compileKotlin')
    doFirst {
        options.compilerArgs = ['--module-path', classpath.asPath,]
        classpath = files()
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
//    archives javadocJar
}

jar {
    inputs.property("moduleName", moduleName)
//    manifest.attributes('Automatic-Module-Name': moduleName)
}

shadowJar.archiveClassifier = 'all'
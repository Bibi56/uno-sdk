import static org.gradle.internal.os.OperatingSystem.*

plugins {
    id 'maven'
    id 'java'
    id 'java-library'
    id "org.jetbrains.kotlin.jvm"
    id "com.github.johnrengelman.shadow"
}

ext {
    moduleName = 'com.github.kotlin_graphics.uno_core'
    kotlin = 'org.jetbrains.kotlin:kotlin'
    kotlin_version = '1.3.41'
    kotlintest_version = '3.3.3'
    gln_version = '342bb4e960cec016dcf23228ef6510cc6ed96aee'
    gli_version = '7215fca3cc82f8757b29f1a6b5e5527555ccd0ba'
    glm_version = '148636ff5d0d7d744707bc39acc7fa7bf5bc4dd8'
    kool_version = '12dabfead107537474721bbd84ba148ca339ee9b'
    unsigned_version = '514405a59b36e8bf2420a0bd3bc6a7ac97dde3c7'
    lwjgl_version = "3.2.2"
    lwjgl_natives = current() == WINDOWS ? "windows" : current() == LINUX ? "linux" : "macos"
}

dependencies {

    implementation "$kotlin-stdlib-jdk8"
    implementation "$kotlin-reflect"

    def kx = "com.github.kotlin-graphics"
    api "${kx}:gln:$gln_version".toString()
    api "${kx}:gli:$gli_version".toString()
    api "${kx}.glm:glm:$glm_version".toString()
    api "${kx}:kool:$kool_version".toString()
    api "${kx}:kotlin-unsigned:$unsigned_version".toString()

    testImplementation "io.kotlintest:kotlintest-runner-junit5:$kotlintest_version".toString()

    ["", "-glfw", "-opengl"].each {
        implementation "org.lwjgl:lwjgl$it:$lwjgl_version"
        runtime "org.lwjgl:lwjgl$it:$lwjgl_version:natives-$lwjgl_natives"
    }
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

compileKotlin {
    // Enable Kotlin compilation to Java 8 class files with method parameter name metadata
    kotlinOptions {
        freeCompilerArgs = ["-XXLanguage:+InlineClasses", "-Xjvm-default=enable"]
        jvmTarget = "1.8"
//        javaParameters = true
    }
    // As per https://stackoverflow.com/a/47669720
    // See also https://discuss.kotlinlang.org/t/kotlin-support-for-java-9-module-system/2499/9
    destinationDir = compileJava.destinationDir
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
//        javaParameters = true
    }
}

compileJava {
    dependsOn(':compileKotlin')
    doFirst {
        options.compilerArgs = ['--module-path', classpath.asPath,]
        classpath = files()
    }
}